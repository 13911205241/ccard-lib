import os

ver = '0.1'
name = 'ccard-lib'
libdir = '$PREFIX/usr/lib/'

env = Environment(
        PREFIX = GetOption('prefix'),
        CPPPATH = ["#/include"],
        CCFLAGS = ["-Wall", "-Wextra", "-Werror", "-g3", "-std=c99"]
        )
# to comply with travis's compiler setting
env["CC"] = os.getenv("CC") or env["CC"]
env["CXX"] = os.getenv("CXX") or env["CXX"]
env["ENV"].update(x for x in os.environ.items() if x[0].startswith("CCC_"))

srcs = Glob("*.c")
libname = name + '.' + ver
slname = env.subst('$LIBPREFIX') + name + env.subst('$LIBSUFFIX')
dlname = env.subst('$SHLIBPREFIX') + name + env.subst('$SHLIBSUFFIX')

sl = env.StaticLibrary(libname, srcs)
dl = env.SharedLibrary(libname, srcs)

sl_new = env.Command("../" + str(sl[0]), sl, Move("$TARGET", "$SOURCE"))
dl_new = env.Command("../" + str(dl[0]), dl, Move("$TARGET", "$SOURCE"))

env.Install(libdir, [sl_new, dl_new])

# make version-less link to static and shared libraries
cmd_ln_static = 'ln -s '+str(sl[0])+' '+slname
cmd_ln_dso = 'ln -s '+str(dl[0])+' '+dlname

env.Command("../"+slname, sl_new, cmd_ln_static, chdir=1)
env.Command("../"+dlname, dl_new, cmd_ln_dso, chdir=1)

env.Command(libdir+slname, libdir+str(sl[0]), cmd_ln_static, chdir=1)
env.Command(libdir+dlname, libdir+str(dl[0]), cmd_ln_dso, chdir=1)

# make 'install' the alias of library directory target
env.Alias('install', libdir)

# vi:ft=python ts=4 sw=4 et fdm=marker
